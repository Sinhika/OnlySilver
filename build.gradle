buildscript {
  repositories {
    jcenter()
    maven { url 'http://files.minecraftforge.net/maven' }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
  }
}
plugins {
  id 'com.github.ben-manes.versions' version '0.13.0'
  id 'com.jfrog.bintray' version '1.7.1'
  id 'com.matthewprenger.cursegradle' version '1.0.8'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

ext.coreModManifest = {
  attributes 'FMLCorePlugin': 'zotmc.onlysilver.loading.OnlyLoading'
  attributes 'FMLCorePluginContainsFMLMod': 'true'
}
apply from: 'bintray.gradle'
apply from: 'curseforge.gradle'

minecraft {
  version = '1.10.2-12.18.1.2076'
  runDir = 'run'
  mappings = 'snapshot_20160518'
}

jar.manifest coreModManifest as Closure
sourceJar.classifier = 'srg-sources'

processResources.with {
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version': project.version
  }
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
}

[compileJava, javadoc].each {
  it.options.encoding = 'UTF-8'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8
