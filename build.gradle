buildscript {
  repositories {
    jcenter()
    maven { url 'http://files.minecraftforge.net/maven' }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
  }
}

plugins {
  id 'com.jfrog.bintray' version '1.6'
  id 'com.matthewprenger.cursegradle' version '1.0.7'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

ext.coreModManifest = {
  attributes 'FMLCorePlugin': 'zotmc.onlysilver.loading.OnlyLoading'
  attributes 'FMLCorePluginContainsFMLMod': 'true'
}
apply from: 'bintray.gradle'
apply from: 'curseforge.gradle'

minecraft {
  version = '1.9.4-12.17.0.1937'
  runDir = 'run'
  mappings = 'snapshot_20160518'
}

jar.manifest coreModManifest as Closure
sourceJar.classifier = 'srg-sources'

processResources.with {
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version': project.version
  }
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
}

[compileJava, javadoc].forEach{
  it.options.encoding = 'UTF-8'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8
